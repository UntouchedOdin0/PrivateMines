plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'io.github.gradle-nexus.publish-plugin' version "1.1.0"
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'org.jetbrains.kotlin.jvm' version '1.7.22'
    id 'net.minecrell.plugin-yml.bukkit' version '0.5.3'
}

group 'me.untouchedodin0.privatemines'
version = pluginVersion


def devBuild = false

if (devBuild) {
    version = version + "-DEV"
}

repositories {
    mavenLocal()
    mavenCentral()

    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    maven { url = 'https://maven.enginehub.org/repo/' }
    maven { url = 'https://repo.aikar.co/content/groups/aikar/' }
    maven { url = 'https://repo.rapture.pw/repository/maven-releases/' }
    maven { url = 'https://repo.rapture.pw/repository/maven-snapshots/' }
    maven { url = 'https://repo.codemc.org/repository/maven-public/' }
    maven { url = 'https://redempt.dev' }
    maven { url = 'https://hub.jeff-media.com/nexus/repository/jeff-media-public/' }
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://libraries.minecraft.net/' }
    maven { url = 'https://repo.convallyria.com/releases' }
    maven { url = 'https://repo.minebench.de/' }
    maven { url = 'https://repo.dmulloy2.net/repository/public/' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url = 'https://repo.mineinabyss.com/releases' }
    maven { url = "https://repo.mattstudios.me/artifactory/public/" }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    compileOnly 'org.spigotmc:spigot-api:1.13.2-R0.1-SNAPSHOT'
    compileOnly 'com.fastasyncworldedit:FastAsyncWorldEdit-Core:2.5.1'
    compileOnly 'com.fastasyncworldedit:FastAsyncWorldEdit-Bukkit:2.5.0'
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.7'
    compileOnly 'com.grinderwolf:slimeworldmanager-api:2.7.0-SNAPSHOT'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7.1'
    compileOnly 'com.mojang:authlib:1.11'
    compileOnly("net.dv8tion:JDA:5.0.0-alpha.12") {
        exclude module: 'opus-java'
    }

    compileOnly 'me.clip:placeholderapi:2.11.3'
    compileOnly 'com.github.oraxen:oraxen:1.155.0'
    compileOnly 'com.github.LoneDev6:API-ItemsAdder:3.4.1-r4'
    compileOnly 'com.google.code.gson:gson:2.10'
    compileOnly 'com.fasterxml.jackson.core:jackson-databind:2.14.1' // i think this is the big thing?

    compileOnly files('libs/XPrison-1.12.11-RELEASE.jar')
    compileOnly files('libs/AutoSellAPI-1.2.0.jar')

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'com.github.Redempt:RedLib:6.5.8'
    implementation 'org.bstats:bstats-bukkit:3.0.0'
    implementation 'net.kyori:adventure-text-minimessage:4.13.0'
    implementation 'net.kyori:adventure-platform-bukkit:4.3.0'
    implementation 'co.aikar:acf-paper:0.5.1-SNAPSHOT'
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'
    implementation "dev.triumphteam:triumph-gui:3.1.2" // Replace version here

    implementation files('libs/PrivateMinesAPI-1.3-SNAPSHOT.jar')
}

shadowJar {
    minimize()
    relocate 'org.codemc.worldguardwrapper', 'me.untouchedodin0.privatemines.worldguardwrapper'
    relocate 'org.bstats', 'me.untouchedodin0.bstats'
    relocate 'net.kyori', 'me.untouchedodin0.adventure'
    relocate 'co.aikar.commands', 'me.untouchedodin0.privatemines.acf'
    relocate 'dev.triumphteam.gui', 'me.untouchedodin0.privatemines.gui'
}

bukkit {
    // Plugin main class (required)
    main = 'me.untouchedodin0.privatemines.PrivateMines'
    version = pluginVersion

    // API version (should be set for 1.13+)
    apiVersion = '1.13'
    authors = ['UntouchedOdin0', 'BristerMitten', 'bobby29831', 'DrZoddiak', 'ComputerNerd100', 'bbSnavy']
    depend = ['FastAsyncWorldEdit', 'WorldGuard', 'Vault']
    softDepend = ['AutoSell', 'UltraPrisonCore', 'ProtocolLib', 'PlaceholderAPI', 'Oraxen', 'ItemsAdder']
}

tasks.register('copyTo_DevServer', Copy) {
    from shadowJar
    into "E:/Coding/Spigot Test Servers/1.19.2 Privates Mines (Paper)/plugins"
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}